plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD', '--grep=^Merge .*$', '--invert-grep'
            standardOutput = stdout
        }
        def versionCode = Integer.parseInt(stdout.toString().trim())
        return versionCode
    }
    catch (ignored) {
        return 0
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        def versionName =  stdout.toString().trim()
        return versionName
    }
    catch (ignored) {
        return '0.0'
    }
}

android {
    namespace 'dsl.crossconf'
    compileSdk 34

    defaultConfig {
        applicationId "dsl.crossconf"
        minSdk 24
        targetSdk 34
        versionCode getVersionCode()
        versionName getVersionName()
        setProperty("archivesBaseName", "version $versionName.$versionCode")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), '../proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(':launch')
    implementation project(':main')
    implementation project(':shared')
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

tasks.register('printVersion') {
    println("Version: ${project.android.defaultConfig.versionName}.${project.android.defaultConfig.versionCode}")
}